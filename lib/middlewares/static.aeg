import "lib/middlewares/middleware.aeg"
import "lib/http.aeg"
import "stdlib/file.aeg"

class StaticMiddleware implements Middleware {
    public prefix = "/static"
    public root = "static"

    init(prefix, root) {
        if (prefix != null) {
            this.prefix = prefix
        }

        if (root != null) {
            this.root = root
        }
    }

    public handle(req) {
        if (req.path.starts_with(this.prefix)) {
            if (req.path.contains("..")) {
                return new Response("Forbidden", 403)
            }

            var relative_path = req.path.slice(this.prefix.len(), req.path.len())
            var file_path = this.root + relative_path

            if (File.exists(file_path)) {
                var content = null
                var mime = "text/plain"
                var is_binary = false

                // Détection du type
                if (file_path.ends_with(".css")) { 
                    mime = "text/css" 
                }
                else if (file_path.ends_with(".js")) { 
                    mime = "application/javascript" 
                }
                else if (file_path.ends_with(".html")) { 
                    mime = "text/html" 
                }
                // Images -> BINAIRE
                else if (file_path.ends_with(".png")) { 
                    mime = "image/png"
                    is_binary = true 
                }
                else if (file_path.ends_with(".jpg")) { 
                    mime = "image/jpeg"
                    is_binary = true 
                }
                else if (file_path.ends_with(".jpeg")) { 
                    mime = "image/jpeg"
                    is_binary = true 
                }
                else if (file_path.ends_with(".ico")) { 
                    mime = "image/x-icon" 
                    is_binary = true 
                }

                // Lecture adaptée
                if (is_binary) {
                    content = File.read_bytes(file_path)
                } else {
                    content = File.read(file_path)
                }

                if (content == null) {
                    print "[Static] Error reading " + file_path
                    return null
                }

                var res = new Response(content, null)
                res.content_type = mime
                
                return res
            }
        }

        return null
    }
}