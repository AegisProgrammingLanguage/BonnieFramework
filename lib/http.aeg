class Request {
    public method: string = "GET"
    public path: string = "/"
    public headers = {}
    public body: string = ""
    public params = {}

    // Parses a raw HTTP string (e.g., "GET /home HTTP/1.1")
    init(raw_data: string) {
        var parts = raw_data.split("\r\n\r\n")
        var header_part = parts.at(0)

        if (parts.len() > 1) {
            this.body = parts.at(1)
        }

        var lines = header_part.split("\r\n")

        if (lines.len() > 0) {
            // 1. Request Line
            var req_line = lines.at(0).split(" ")
            if (req_line.len() >= 2) {
                this.method = req_line.at(0)
                this.path = req_line.at(1)
            }

            // 2. Parse Headers
            foreach (i in 1..lines.len()) {
                var line = lines.at(i)
                var split_idx = line.index_of(":")
                if (split_idx != -1) {
                    var key = line.split(": ").at(0) 
                    var val = line.split(": ").at(1)
                    this.headers.insert(key, val)
                }
            }
        }
    }
}

class Response {
    public status_code: int = 200
    public content_type: string = "text/html"
    public headers = {}
    public body: string = "text/plain"

    init(body, status) {
        this.body = body
        if (status != null) {
            this.status_code = status
        }
    }

    public set_header(key, value) {
        this.headers.insert(key, value)
    }

    public get_headers_string() {
        var s = "HTTP/1.1 " + this.status_code + " OK\r\n"
        
        // On s'assure que Content-Type est dÃ©fini
        if (!this.headers.contains("Content-Type")) {
            this.headers.insert("Content-Type", this.content_type)
        }

        // Calcul automatique de la taille du contenu
        if (!this.headers.contains("Content-Length")) {
            // .len() fonctionne sur String ET sur Bytes maintenant :)
            this.headers.insert("Content-Length", to_str(this.body.len()))
        }

        foreach (key in this.headers.keys()) {
            s += key + ": " + this.headers.get(key) + "\r\n"
        }

        s += "\r\n" // Fin des headers
        return s
    }

    // Converts the object to a valid HTTP wire string
    public to_string() {
        return this.get_headers_string() + to_str(this.body)
    }
}
